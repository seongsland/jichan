plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.12'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'jacoco'
}

group = 'com.jichan'
version = '1.0.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }

}

// QueryDSL Build 경로 추가
sourceSets {
    main {
        java {
            srcDirs = ["$projectDir/src/main/java", "$projectDir/build/generated/querydsl"]
        }
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    implementation 'io.jsonwebtoken:jjwt-impl:0.12.3'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.12.3'

    // QueryDSL
    // 그룹 ID를 com.querydsl에서 io.github.openfeign.querydsl로 변경
    // 버전은 취약점이 해결된 5.6.1 사용
    implementation 'io.github.openfeign.querydsl:querydsl-jpa:5.6.1:jakarta'
    annotationProcessor 'io.github.openfeign.querydsl:querydsl-apt:5.6.1:jakarta'
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    systemProperty "file.encoding", "UTF-8"
}


def querydslDir = layout.buildDirectory.dir("generated/querydsl").get().asFile

tasks.withType(JavaCompile).configureEach {
    options.generatedSourceOutputDirectory.set(file(querydslDir))
}

sourceSets {
    main {
        java {
            srcDirs += querydslDir
        }
    }
}

clean {
    delete querydslDir
}

jar {
    enabled = false
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    dependsOn test // 리포트 생성을 위해 테스트가 먼저 실행되어야 함

    reports {
        xml.required = false
        csv.required = false
        html.required = true // 우리가 눈으로 볼 HTML 리포트 생성
    }

    // 리포트에서 제외할 클래스들 (QueryDSL Q클래스 등)
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "**/Q*",
                    "**/config/**",
                    "**/dto/**",
                    "**/entry/**",
                    "**/exception/**",
                    "**/repository/**",
                    "**/security/**"
            ])
        }))
    }
}